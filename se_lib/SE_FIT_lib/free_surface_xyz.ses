// preb_freesurf_yzx.txt
// Initial free surface that can be used actually in any closed container
// cylinder center line along x-axis
// Yongkang Chen, Portland State University
// Dec 14, 2008
//
// essential parameters:     rad: the radius of the cylinder,
//                       len_cyl: the half length of the cylinder
//                        long_a: semimajor axis of the upper dome
//                        long_b: semimajor axis of the lower dome

// for computing vol_t, this code can be easily modified to fit other geometries.
//
//
//                2_____e1______1
//		   /|            /|
//		e2/ |        e4 / |
//             /  |e10       /  |e9
//	      3/___|__e3____4/   |
//           |    |        |    |
//           |    |6____e5_|___ |5
//        e11|   /         |   /
//           |e6/       e12|  /e8
//           | /           | /
//	     7|/___________8|/
//                 e7
// quietload on

define freesurf_v_id integer[8]
define freesurf_e_id integer[12]
define freesurf_f_id integer[6]
define freesurf_b_id integer[1]

freesurf_v_id[1] := new_vertex(x_center + cube_side_length_half, y_center + cube_side_length_half, z_center + cube_side_length_half)
freesurf_v_id[2] := new_vertex(x_center - cube_side_length_half, y_center + cube_side_length_half, z_center + cube_side_length_half)
freesurf_v_id[3] := new_vertex(x_center - cube_side_length_half, y_center - cube_side_length_half, z_center + cube_side_length_half)
freesurf_v_id[4] := new_vertex(x_center + cube_side_length_half, y_center - cube_side_length_half, z_center + cube_side_length_half)
freesurf_v_id[5] := new_vertex(x_center + cube_side_length_half, y_center + cube_side_length_half, z_center - cube_side_length_half)
freesurf_v_id[6] := new_vertex(x_center - cube_side_length_half, y_center + cube_side_length_half, z_center - cube_side_length_half)
freesurf_v_id[7] := new_vertex(x_center - cube_side_length_half, y_center - cube_side_length_half, z_center - cube_side_length_half)
freesurf_v_id[8] := new_vertex(x_center + cube_side_length_half, y_center - cube_side_length_half, z_center - cube_side_length_half)

freesurf_e_id[1] := new_edge(freesurf_v_id[1],freesurf_v_id[2])
freesurf_e_id[2] := new_edge(freesurf_v_id[2],freesurf_v_id[3])
freesurf_e_id[3] := new_edge(freesurf_v_id[3],freesurf_v_id[4])
freesurf_e_id[4] := new_edge(freesurf_v_id[4],freesurf_v_id[1])
freesurf_e_id[5] := new_edge(freesurf_v_id[5],freesurf_v_id[6])
freesurf_e_id[6] := new_edge(freesurf_v_id[6],freesurf_v_id[7])
freesurf_e_id[7] := new_edge(freesurf_v_id[7],freesurf_v_id[8])
freesurf_e_id[8] := new_edge(freesurf_v_id[8],freesurf_v_id[5])
freesurf_e_id[9] := new_edge(freesurf_v_id[1],freesurf_v_id[5])
freesurf_e_id[10] := new_edge(freesurf_v_id[2],freesurf_v_id[6])
freesurf_e_id[11] := new_edge(freesurf_v_id[3],freesurf_v_id[7])
freesurf_e_id[12] := new_edge(freesurf_v_id[4],freesurf_v_id[8])
for (id_ind := 1; id_ind < 13; id_ind+=1) {set edge[freesurf_e_id[id_ind]] color red}

freesurf_b_id[1] := new_body

freesurf_f_id[1] := new_facet( freesurf_e_id[1], freesurf_e_id[2],  freesurf_e_id[3], freesurf_e_id[4])
freesurf_f_id[2] := new_facet(-freesurf_e_id[1], freesurf_e_id[9],  freesurf_e_id[5],-freesurf_e_id[10])
freesurf_f_id[3] := new_facet(-freesurf_e_id[2], freesurf_e_id[10], freesurf_e_id[6],-freesurf_e_id[11])
freesurf_f_id[4] := new_facet(-freesurf_e_id[3], freesurf_e_id[11], freesurf_e_id[7],-freesurf_e_id[12])
freesurf_f_id[5] := new_facet(-freesurf_e_id[4], freesurf_e_id[12], freesurf_e_id[8],-freesurf_e_id[9])
freesurf_f_id[6] := new_facet(-freesurf_e_id[5],-freesurf_e_id[8], -freesurf_e_id[7],-freesurf_e_id[6])
freesurf_setup := 	{for (id_ind := 1; id_ind < 7; id_ind+=1) 
				{set facet where original == freesurf_f_id[id_ind] tension 1; 
				 set facet where original == freesurf_f_id[id_ind] frontbody freesurf_b_id[1];
				 set facet where original == freesurf_f_id[id_ind] color lightgray;
				 /*set facet[freesurf_f_id[id_ind]] quantity mygrav; */
				 /*set facet[freesurf_f_id[id_ind]] quantity xmc; */
				 /*set facet[freesurf_f_id[id_ind]] quantity ymc; */
				 /*set facet[freesurf_f_id[id_ind]] quantity zmc; */
				};
			}
freesurf_setup

/* set facet frontbody freesurf_b_id[1] where color == red */
set body[freesurf_b_id[1]] target (1-fill_fraction)*vol_total
set body[freesurf_b_id[1]] density 0

s
R
2r2d
q


